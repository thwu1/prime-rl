 #lang racket

;; Interpreters for each IR in irs.rkt -- updated for the new R5 pipeline.
(require "irs.rkt")
(require "system.rkt")

(provide (all-defined-out))

;; In this case we provide three interpreters:
;; - R5 -- works for shrink, uniqueify, assignment-convert, and anf-convert
;; - blocks -- works for explicate-control, uncover-locals
;; - instr -- works for select-instructions, assign-homes, patch-instructions, prelude-and-conclusion

;; ────────────────────────────────────────────────────────────────────────────
;; Helpers
;; ────────────────────────────────────────────────────────────────────────────

(define (display-return v) (displayln v) v)

(define (next-input in)
  (unless (pair? in)
    (error 'interpret "input exhausted for (read) / _read_int64"))
  (cons (car in) (cdr in)))

(define (atom-val a env)
  (cond [(fixnum? a) a]
        [(symbol? a) (hash-ref env a (λ () (error 'interpret "unbound id ~a" a)))]
        [(boolean? a) a]
        [(equal? a '(void)) (void)]
        [else (error 'interpret "bad atom ~a" a)]))

(define (expect-int v who)
  (if (fixnum? v) v (error who "expected Int, got ~a" v)))

(define (expect-bool v who)
  (if (boolean? v) v (error who "expected Bool, got ~a" v)))

(define (apply-binary op v0 v1 who)
  (match op
    ['+   (+ (expect-int v0 who) (expect-int v1 who))]
    ['-   (- (expect-int v0 who) (expect-int v1 who))]
    ['eq? (equal? v0 v1)] ;; permit non-ints to be eq?d 
    ['<   (< (expect-int v0 who) (expect-int v1 who))]
    ['<=  (<= (expect-int v0 who) (expect-int v1 who))]
    ['>   (> (expect-int v0 who) (expect-int v1 who))]
    ['>=  (>= (expect-int v0 who) (expect-int v1 who))]
    [_ (error who "unsupported binary op ~a" op)]))

  (define (merge h0 h1)
    (foldl (λ (k0 h1) (hash-set h1 k0 (hash-ref h0 k0))) h1 (hash-keys h0)))

;; ────────────────────────────────────────────────────────────────────────────
;; R5 / shrunk-R5 / unique-source-tree / ANF (source-level interpreter)
;;   - Supports while, begin, set!, make-vector, vector-ref, vector-set!
;; ────────────────────────────────────────────────────────────────────────────

(define r3-binary-ops '(+ - and or eq? < <= > >=))

(define (eval-R5-binary op e0 e1 env in)
  (cond
    [(equal? op 'and)
     (match (eval-R5-exp e0 env in)
       [(cons v0 in1)
        (expect-bool v0 'and)
        (if v0 (eval-R5-exp e1 env in1) (cons #f in1))])]
    [(equal? op 'or)
     (match (eval-R5-exp e0 env in)
       [(cons v0 in1)
        (expect-bool v0 'or)
        (if v0 (cons #t in1) (eval-R5-exp e1 env in1))])]
    [else
     (match (eval-R5-exp e0 env in)
       [(cons v0 in1)
        (match (eval-R5-exp e1 env in1)
          [(cons v1 in2) (cons (apply-binary op v0 v1 'R5) in2)])])]))

(define (eval-R5-begin es env in)
  (match es
    ['() (cons 0 in)]
    [`(,e) (eval-R5-exp e env in)]
    [`(,e . ,rest)
     (match (eval-R5-exp e env in)
       [(cons _ in1) (eval-R5-begin rest env in1)])]))

(define (eval-R5-while g b env in)
  (let loop ([env env] [in in])
    (match (eval-R5-exp g env in)
      [(cons vg in1)
       (if (expect-bool vg 'while)
           (match (eval-R5-exp b env in1)
             [(cons _ in2) (loop env in2)])
           (cons 0 in1))])))

(define (eval-R5-exp e env in)
  (match e
    [#t                              (cons #t in)]
    [#f                              (cons #f in)]
    [(? fixnum? n)                   (cons n in)]
    ['(read)                         (next-input in)]
    ['(void)                         (cons (void) in)]
    ;; unary
    [`(- ,e0)
     (match (eval-R5-exp e0 env in)
       [(cons v in*) (cons (- (expect-int v 'unary-)) in*)])]
    [`(not ,e0)
     (match (eval-R5-exp e0 env in)
       [(cons v in*) (cons (not (expect-bool v 'not)) in*)])]
    ;; binary
    [`(,op ,e0 ,e1) #:when (member op r3-binary-ops)
                    (eval-R5-binary op e0 e1 env in)]
    ;; control
    [`(if ,e-g ,e-t ,e-f)
     (match (eval-R5-exp e-g env in)
       [(cons vg in1)
        (if (expect-bool vg 'if)
            (eval-R5-exp e-t env in1)
            (eval-R5-exp e-f env in1))])]
    ;; sequencing and loops
    [`(begin ,es ... ,ret)          (eval-R5-begin (append es (list ret)) env in)]
    [`(while ,g ,b)                 (eval-R5-while g b env in)]
    ;; vectors
    [`(make-vector ,e-len)
     (match (eval-R5-exp e-len env in)
       [(cons len in1) (cons (make-vector len) in1)])]
    [`(vector-ref ,e-v ,e-i)
     (match (eval-R5-exp e-v env in)
       [(cons vv in1)
        (match (eval-R5-exp e-i env in1)
          [(cons vi in2) (cons (vector-ref vv vi) in2)])])]
    [`(vector-set! ,e-v ,e-i ,e-val)
     (match (eval-R5-exp e-v env in)
       [(cons vv in1)
        (match (eval-R5-exp e-i env in1)
          [(cons vi in2)
           (match (eval-R5-exp e-val env in2)
             [(cons v3 in3) (vector-set! vv vi v3) (cons 0 in3)])])])]
    ;; variables / let / set!
    [(? symbol? x)
     (cons (vector-ref (hash-ref env x (λ () (error 'interpret "unbound id ~a" x))) 0)  in)]
    [`(let ([,(? symbol? x) ,rhs]) ,body)
     (define cell (make-vector 1))
     (match (eval-R5-exp rhs env in)
       [(cons v in*)
        (vector-set! cell 0 v)
        (eval-R5-exp body (hash-set env x cell) in*)])]
    [`(set! ,(? symbol? x) ,rhs)
     (match (eval-R5-exp rhs env in)
       [(cons v in*)
        (vector-set! (hash-ref env x) 0 v)
        (cons '(void) in*)])]
    [`(app ,e-f ,args ...)
     (eval-R5-exp `(,e-f ,@args) env in)]
    [`(fun-ref ,f) (eval-R5-exp f env in)]
    [`(lambda (,xs ...) ,e+) `((clo ,e ,env) . ,in)]
    [`(,e-f ,e-args ...)
     (match-define `((clo (lambda (,xs ...) ,e-b) ,env+) . ,in+) (eval-R5-exp e-f env in))
     (match-define `(,reversed-vs ,in++)
       (foldl (lambda (e-arg acc)
                (match-define `(,acc-vs ,in) acc)
                (match-define `(,v-result . ,in+) (eval-R5-exp e-arg env in))
                `(,(cons v-result acc-vs) ,in+))
              `(() ,in+)
              e-args))
     (define vs (map (lambda (x) (vector x)) (reverse reversed-vs)))
     (define env++ (foldl (lambda (x v h) (hash-set h x v)) env+ xs vs))
     (eval-R5-exp e-b env++ in++)]
    [_ (error 'interpret "malformed R5 expression: ~a" e)]))

(define (interpret-R5 p [in (range 10000)])
  (define (build-env defn env)
    (match defn
      [`(define (,f ,args ...) ,e-b)
       (hash-set env f (vector `(clo (lambda (,@args) ,e-b) ,(hash))))]))
  (define (build-env* defns)
    (foldl build-env (hash) defns))
  (define defns (match p
                  [`(program ,defns ...) defns]
                  [`(program ,defns ... ,(? R5-exp? main)) defns]))
  (define env
    (let* ([cells
            ;; List of (f . cell)
            (for/list ([defn defns])
              (match defn
                [`(define (,f ,args ...) ,e-b)
                 (cons f (make-vector 1))]))]
           [env
            ;; Immutable hash: f ↦ cell
            (for/fold ([h (hash)]) ([fc cells])
              (hash-set h (car fc) (cdr fc)))])
      ;; Second pass: fill each cell with a closure that captures the whole env.
      (for ([defn defns])
        (match defn
          [`(define (,f ,args ...) ,e-b)
           (define cell (hash-ref env f))
           (vector-set! cell 0 `(clo (lambda (,@args) ,e-b) ,env))]))
      env))
  ;; interpret the program
  (define res
    (match p
      [`(program ,defns ...)
       (eval-R5-exp `(main) env in)]
      [`(program ,defns ... ,(? R5-exp? main))
       (eval-R5-exp main env in)]))
  (display-return (car res)))

;; ────────────────────────────────────────────────────────────────────────────
;; BLOCKS (explicate-control and uncover-locals):
;;   - RHS supports: read, -, +, not, eq?, <, (vector aLen), (vector-ref a i)
;;   - Statement form in tails: (vector-set! a i v)
;;   - IF form: (if (<|eq? a0 a1) (goto lt) (goto lf))
;; ────────────────────────────────────────────────────────────────────────────

(define (rhs-val rhs env in)
  (match rhs
    [(? fixnum? n)                        (cons n in)]
    [(? boolean? b)                       (cons b in)]
    [(? symbol? x)                        (cons (hash-ref env x (λ () (error 'interpret "unbound id ~a" x))) in)]
    ['(void)                              (cons (void) in)]
    ['(read)                              (next-input in)]
    [`(- ,a)
     (cons (- (expect-int (atom-val a env) 'BLOCKS-unary-)) in)]
    [`(not ,a)                            (cons (not (atom-val a env)) in)]
    [`(+ ,a0 ,a1)
     (cons (+ (expect-int (atom-val a0 env) 'BLOCKS/+)
              (expect-int (atom-val a1 env) 'BLOCKS/+)) in)]
    [`(eq? ,a0 ,a1)
     (cons (equal? (atom-val a0 env) (atom-val a1 env)) in)]
    [`(< ,a0 ,a1)
     (cons (< (expect-int (atom-val a0 env) 'BLOCKS/<)
              (expect-int (atom-val a1 env) 'BLOCKS/<)) in)]
    [`(make-vector ,i)
     (cons (make-vector (expect-int (atom-val i env) 'BLOCKS/vector)) in)]
    [`(vector-ref ,a0 ,i)
     (cons (vector-ref (atom-val a0 env)
                       (expect-int (atom-val i env) 'BLOCKS/vector-ref)) in)]
    [`(fun-ref ,f)     (cons rhs in)]
    [_                                    (error 'interpret "bad BLOCKS rhs ~a" rhs)]))

(define (exec-blocks blocks name->args label toplevel-env in)
  (define (go s env stack in)
    (match s
      [`(return ,a)                         
       (match stack
         ['(top-stack) 
          (match (rhs-val a env in) [(cons v in*) (cons v in*)])]
         [`((,x ,env+ ,rst) . ,stack+)
          (match (rhs-val a env in)
            [(cons v in*) (go rst (hash-set env+ x v) stack+ in*)])])]
      [`(seq (assign ,x (app ,f ,args ...)) ,rst)
       (match-define (cons `(fun-ref ,fptr) _) (rhs-val f env in))
       (define vs (map (lambda (a) (atom-val a env)) args))
       (define env+
         (foldl
          (lambda (x v acc) (hash-set acc x v))
          toplevel-env
          (hash-ref name->args fptr)
          vs))
       (go (hash-ref blocks fptr) env+ (cons `(,x ,env ,rst) stack) in)]
      [`(seq (assign ,(? symbol? x) ,rhs) ,rest)
       (match (rhs-val rhs env in)
         [(cons v in*) (go rest (hash-set env x v) stack in*)])]
      ;; side-effect statement
      [`(seq (vector-set! ,av ,i ,aval) ,rest)
       (vector-set! (atom-val av env) 
              (expect-int (atom-val i env) 'BLOCKS/vector-set!)
              (atom-val aval env))
       (go rest env stack in)]
      ;; generic IF on eq?/</etc.
      [`(if (,cmp ,a0 ,a1) (goto ,(? label? l-t)) (goto ,(? label? l-f)))
       (define v0 (atom-val a0 env))
       (define v1 (atom-val a1 env))
       (define truth
         (match cmp
           ['eq? (equal? v0 v1)]
           ['<   (< (expect-int v0 'BLOCKS/<) (expect-int v1 'BLOCKS/<))]
           [else (error 'interpret "unsupported cmp ~a in BLOCKS if" cmp)]))
       (go (hash-ref blocks (if truth l-t l-f)) env stack in)]
      [`(goto ,(? label? l))               (go (hash-ref blocks l) env stack in)]
      [_                                   (error 'interpret "bad BLOCKS tail ~a" s)]))
  (go (hash-ref blocks label) toplevel-env '(top-stack) in))

(define (interpret-blocks p [in (range 10000)])
  (match-define `(program ,defns ...) p)
  (define name->args
    (foldl (lambda (defn acc) (match defn
                                [`(define (,f ,args ...) ,_) (hash-set acc f args)]
                                [`(define ,_ (,f ,args ...) ,_) (hash-set acc f args)]))
           (hash)
           defns))
  (define blocks (match p 
                   [`(program (define (,f ,args ...) ,blocks) ...) blocks]
                   [`(program (define ,_ (,f ,args ...) ,blocks) ...) blocks]))
  (define flattened-blocks (foldl merge (hash) blocks))
  (display-return (car (exec-blocks flattened-blocks name->args (entry-symbol) (hash) in))))

;; ────────────────────────────────────────────────────────────────────────────
;; (Pseudo-)x86-64 Interpreter
;; ────────────────────────────────────────────────────────────────────────────

;; State is:
;; `(,regs ,locals ,stack ,heap ,flags)
;; 
;; Pointers are represented as `(pointer-to addr)`
;; Addresses are either '(stack-addr i) or '(heap-addr i)
;; Primitive operations are updated to work on pointers

(define (read-op op st)
  (match-define `(,regs ,locals ,stack ,flags) st)
  (match op
    [`(imm ,n)                    n]
    [`(reg ,r)                    (hash-ref regs r 0)]
    [`(byte-reg ,r)               (hash-ref regs r 0)]
    [`(var ,x)                    (hash-ref locals x (λ () (error 'interp-instrs "unbound var ~a" x)))]
    [`(deref (reg rbp) ,off)      (hash-ref locals off)]
    [`(deref (reg ,r) ,off)
     (match (read-op `(reg ,r) st)
       [(? vector? v) (vector-ref v (/ (- off 8) 8))])]))

(define (write-op op v st)
  (match-define `(,regs ,locals ,stack ,flags) st)
  (match op
    [`(reg ,r)                    `(,(hash-set regs r v) ,locals ,stack  ,flags)]
    [`(byte-reg ,r)               `(,(hash-set regs r (bitwise-and v #xFF)) ,locals ,stack ,flags)]
    [`(var ,x)                    `(,regs ,(hash-set locals x v)  ,stack ,flags)]
    [`(deref (reg rbp) ,off)      `(,regs ,(hash-set locals off v) ,stack ,flags)]
    [`(deref (reg ,r) ,off)        
     (define vec (read-op `(reg ,r) st))
     (define idx (/ (- off 8) 8))
     (match vec
       [(? vector?) (vector-set! vec idx v)])
     `(,regs ,locals ,stack ,flags)]
    [_ (error 'interp-instrs "cannot write to ~a" op)]))

(define (cmp-flags srcv dstv)
  (define src+ (if (number? srcv) srcv (equal-hash-code srcv)))
  (define dst+ (if (number? dstv) dstv (equal-hash-code dstv)))
  (define res (- dst+ src+))
  (define sign (λ (x) (if (< x 0) 1 0)))
  (define of? (and (not (= (sign dst+) (sign src+)))
                   (not (= (sign dst+) (sign res)))))
  (hash 'ZF (= res 0) 'SF (< res 0) 'OF of?))

(define (cc-true? cc flags)
  (match cc
    ['e  (hash-ref flags 'ZF #f)]
    ['l  (let ([ZF (hash-ref flags 'ZF #f)]
               [SF (hash-ref flags 'SF #f)]
               [OF (hash-ref flags 'OF #f)])
           (and (not ZF) (not (equal? SF OF))))] ; signed less-than
    ['le (let ([ZF (hash-ref flags 'ZF #f)]
               [SF (hash-ref flags 'SF #f)]
               [OF (hash-ref flags 'OF #f)])
           (or ZF (not (equal? SF OF))))]
    ['g  (let ([ZF (hash-ref flags 'ZF #f)]
               [SF (hash-ref flags 'SF #f)]
               [OF (hash-ref flags 'OF #f)])
           (and (not ZF) (equal? SF OF)))]
    ['ge (equal? (hash-ref flags 'SF #f) (hash-ref flags 'OF #f))]
    [_ (error 'interp-instrs "unsupported cc ~a" cc)]))

(define out? #f)

(define (interp-tail instrs blocks st in)
  (match-define `(,regs ,locals ,stack ,flags) st)
  (match instrs
    ['() (read-op '(reg rax) st)]
    [`((retq) . ,_) 
     (match stack
       ['(the-stack-stops-here)
        (read-op '(reg rax) st)]
       [`((,locals+ ,rst) . ,stack+) (interp-tail rst blocks `(,regs ,locals+ ,stack+ ,flags) in)])]
    [`((goto ,l) . ,_)
     (interp-tail (hash-ref blocks l) blocks st in)]
    [`((movq ,src ,dst) . ,rst) 
     (interp-tail rst blocks (write-op dst (read-op src st) st) in)]
    [`((movzbq ,src ,dst) . ,rst)
     (interp-tail rst blocks (write-op dst (bitwise-and (read-op src st) #xFF) st) in)]
    [`((addq ,src ,dst) . ,rst)
     (define src-v (read-op src st))
     (define dst-v (read-op dst st))
     (define sum (match dst-v
                   [(? fixnum? n) (+ dst-v src-v)]
                   [`(stack-addr ,v) `(stack-addr ,(+ v src-v))]))
     (interp-tail rst blocks (write-op dst sum st) in)]
    [`((xorq ,src ,dst) . ,rst)
     (define res (bitwise-xor (read-op dst st) (read-op src st)))
     (match-define `(,regs+ ,locals+ ,stack+ ,_) (write-op dst res st))
     (define flags+ (hash 'ZF (= res 0) 'SF (< res 0) 'OF #f))
     (interp-tail rst blocks `(,regs+ ,locals+ ,stack+ ,flags+) in)]
    [`((negq ,op) . ,rst)
     (interp-tail rst blocks (write-op op (- (read-op op st)) st) in)]
    [`((pushq ,src) . ,rst)
     (interp-tail rst blocks `(,regs ,locals ,(cons (read-op src st) stack) ,flags) in)]
    [`((popq ,dst) . ,rst)
     (match stack
       ['() (error 'interp-instrs "pop from empty stack")]
       [`(,top . ,rest)
        (define st* (write-op dst top st))
        (match-define `(,regs+ ,locals+ ,_ ,flags+) st*)
        (interp-tail rst blocks `(,regs+ ,locals+ ,rest ,flags+) in)])]
    [`((cmpq ,a0 ,a1) . ,rst)
     (define flags* (cmp-flags (read-op a0 st) (read-op a1 st)))
     (interp-tail rst blocks `(,regs ,locals ,stack ,flags*) in)]
    [`((set ,cc ,dst) . ,rst)
     (define b (if (cc-true? cc (last st)) 1 0))
     (interp-tail rst blocks (write-op dst b st) in)]
    [`((jmp ,lab) . ,_)
     (interp-tail (hash-ref blocks lab) blocks st in)]
    [`((jmp-if ,cc ,lab) . ,rst)
     (if (cc-true? cc (last st))
         (interp-tail (hash-ref blocks lab) blocks st in)
         (interp-tail rst blocks st in))]
    [`((leaq (fun-ref ,f) ,dst) . ,rst)
     (interp-tail rst blocks (write-op dst `(fun-ref ,f) st) in)]
    [`((callq ,lbl ,_) . ,rst)
     (match (linuxify lbl) 
       ['read_int64
        (define v (car in))
        (interp-tail rst blocks (write-op '(reg rax) v st) (cdr in)) ]
       ['print_int64
        (displayln (read-op '(reg rax) st))
        (set! out? #t)
        (interp-tail rst blocks st in)]
       ['make_vector
        (define i (read-op '(reg rdi) st))
        (define vec (make-vector i))
        (interp-tail rst blocks (write-op '(reg rax) vec st) in)]
       [_ (error 'interp-instrs "unknown call ~a" lbl)])]
    ;; Indirect calls via function pointers
    [`((indirect-callq ,op) . ,rst)
     (define fp (read-op op st))
     (match-define `(fun-ref ,f) fp)
     (interp-tail (hash-ref blocks f)
                  blocks
                  `(,regs ,(hash) ,(cons `(,locals ,rst) stack) ,flags)
                  in)]
    [_ (error 'interp-instrs "unknown instruction sequence ~a" instrs)]))


(define (interpret-instr prog [in (range 10000)])
  (set! out? #f)
  (match prog
    [`(program ,_ ,defns ...)
     (define raw-blocks (match prog
                          [`(program (define (,f ,args ...) ,blocks) ...) blocks]
                          [`(program (define ,_ (,f ,args ...) ,blocks) ...) blocks]))
     (define blocks (foldl merge (hash) raw-blocks))
     (define instrs (hash-ref blocks (entry-symbol)))
     (define init-regs (hash 'rsp '(stack-addr #xAA000000)
                             'rbp '(stack-addr #xAA000000)))
     (define init-state `(,init-regs ,(hash) (the-stack-stops-here) ,(hash 'ZF #f 'SF #f 'OF #f)))
     (define result (interp-tail instrs blocks init-state in))
     (if out? result (begin (displayln result) result))]))

(define (dummy-interp-x86-64 s i) 
  "x86-64 code not interpreted, skipping interpreter for this pass--test by running binary")

;; ────────────────────────────────────────────────────────────────────────────
;; Dispatcher
;; ────────────────────────────────────────────────────────────────────────────

(define (interpret p [in (range 10000)])
  (cond [(R5? p)                                 (interpret-R5 p in)]
        [(shrunk-R5? p)                          (interpret-R5 p in)]
        [(unique-source-tree? p)                 (interpret-R5 p in)]
        [(anf-program? p)                        (interpret-R5 p in)]
        [(blocks-program? p)                     (interpret-blocks p in)]
        [(locals-program? p)                     (interpret-blocks p in)]
        [(instr-program? p)                      (interpret-instr p in)]
        [(homes-assigned-program? p)             (interpret-instr p in)]
        [(patched-program? p)                    (interpret-instr p in)]
        [(x86-64? p)                             (interpret-instr p in)]
        [else (error 'interpret "unknown IR kind")]))

#;
(interpret-R5 '(program
                (define (main) ((f (read)) (read)))
                (define (f x) (lambda (y) (+ y x)))))
